query			<- _ select __ from (__ join)* (__ where)? (__ group_by)? (__ having)? (__ order_by)? (__ offset)?;
select			<- 'SELECT' (__ ('DISTINCT' / 'ALL'))? (__ select_limit)? __ select_list;
select_limit		<- 'TOP' __ unsigned_integer;
select_list		<- select_item (_ ',' _ select_item)*;
select_item		<- select_column (__ ('AS' __)? identifier)?;
select_column		<- subquery / '*' / identifier / numeric_expression / function / search_condition;
from			<- 'FROM' __ (subquery /  from_list);
from_list		<- table_item (_ ',' _ table_item)*;
subquery		<- '(' _ query _ ')' (__ 'AS')? __ identifier;
join			<- (join_method __)? 'JOIN' __ (identifier join_condition / '(' query ')'); 
join_method		<- 'NATURAL'? ('INNER' / (('LEFT' /  'RIGHT' / 'FULL') _ 'OUTER'?))?;
join_condition		<- 'ON' __ '('? search_list ')'? / 'USING' _ '(' identifier (_ ',' identifier)* ')';
where			<- 'WHERE' __ search_list;
search_list		<- search_condition (__ ('AND' / 'OR') ('NOT' __)? __ search_condition)*;
search_condition		<- unary_operation / binary_operation / ternary_operation / list_operation / exists_operation / function; 
where_column		<- identifier / numeric_expression / function;
unary_operation 	<- 'IS' (__ 'NOT')? __ where_column;
binary_operation	<- where_column _ ("=" / "!=" / "<=" / ">=" / ">" / "<" / "I"?"LIKE" / "||") _ where_column;
ternary_operation	<- where_column __ ('NOT' __)? 'BETWEEN' where_column __ 'AND' __ where_column;
list_operation		<- ('NOT' __)? 'IN' (where_column / '(' where_column (_ ',' where_column)* ')');
exists_operation 	<- ('NOT' __)? 'EXISTS' '(' query ')';
table_item		<- identifier (__ ('AS' __)? identifier)?;
order_by		<- 'ORDER BY' __ order_by_list;
order_by_list		<- order_by_item (_ ',' _ order_by_item)*;
order_by_item		<- (identifier / unsigned_integer) (__ order_by_sort)?;
order_by_sort		<- 'ASC' / 'DESC';
group_by		<- 'GROUP BY' __ identifier (_ ',' identifier)*;
having			<- 'HAVING' __ '(' _ search_condition _ ')';
offset 			<- 'OFFSET' __ unsigned_integer;
identifier		<- (regular_identifier / delimited_identifier) ("." (regular_identifier / delimited_identifier / '*'))?;
delimited_identifier	<- r'"' (r'\\"' / r'[^"]')* r'"' / r"'" (r"\\'" / r"[^\']")* r"'";
regular_identifier	<- (!reserved_keywords letter (letter / digit / "_")*);
function 			<- function_name '(' (where_column (_ ',' _ where_column)*)? ')';
function_name		<- 'ABS' / 'ACOS' / 'AREA' / 'ASIN' / 'ATAN' / 'ATAN2' / 'BOX' / 'CEILING' / 'CENTROID' / 'CIRCLE' / 'COS' / 'DEGREES' / 'EXP' / 'FLOOR' / 'LOG' / 'LOG10' / 'MOD' / 'PI' / 'POINT' / 'POLYGON' / 'POWER' / 'RADIANS' / 'REGION' / 'RAND' / 'ROUND' / 'SIN' / 'SQRT' / 'TAN';
comment			<- "-" "-" r'.*\n';
numeric_expression	<- term (_ ('+' / '-') _ numeric_expression)?;
term			<- factor (_ ('*' / '/') _ term)?;
factor			<- ('+' / '-')? numeric_primary;
numeric_primary		<- numeric_value / identifier;
numeric_value		<- scientific_number / decimal / unsigned_integer;
scientific_number	<- (decimal / unsigned_integer) 'E' ('+' / '-')? unsigned_integer;
decimal			<- unsigned_integer '.' digit+;
unsigned_integer	<- digit+;
digit			<- r'[0-9]';
letter			<- r'[a-zA-Z]';
reserved_keywords	<- 'SELECT' / 'FROM' / 'WHERE' / 'ORDER BY' / 'DISTINCT' / 'ALL' / 'TOP' / 'AS' / 'ASC' / 'DESC' / '=' / '!=' / '<=' / '>=' / '>' / '<' / 'LIKE' / 'ILIKE' / 'OR' / 'AND' / 'JOIN' / 'ON' / 'INNER' / 'OUTER' / 'LEFT' / 'RIGHT' / 'OUTER' / 'NATURAL' / 'ON' / 'USING' / 'OFFSET' / 'GROUP BY' / 'HAVING' / '||' / 'EXISTS' / function_name;
ANY_CHAR		<- letter / digit / ' ' / '\t' / ',' / ';' / '.';
_			<- comment / Space* / EOL*;
__			<- comment / Space+ / EOL*;
Space			<- ' '+ / '\t';
EOL			<- '\r\n' / '\n' / '\r';
