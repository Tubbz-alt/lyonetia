// The top-level non-terminal; this is what complete ADQL
// statements must match.
query <- 
	(with_clause __)? query_expression _ EOF;
with_clause <-
	'WITH' __ identifier __ 'AS' __ '(' _ query_expression _ ')' (_ ',' _ '(' _ query_expression _ ')')*;
query_expression <- 
	query_specification
	(__ set_operator __ set_query_expression)*;
set_query_expression <-
	query_expression / '(' _ query_expression _ ')';
query_specification <- 
	_ 'SELECT'
	(__ set_quantifier)?
	(__ set_limit)?
	__ select_list
	__ table_expression;
table_expression <-
	from_clause 
	(__ where_clause)? 
	(__ group_by)? 
	(__ order_by)? 
	(__ offset)?;
set_operator <-
	'EXCEPT' / 'INTERSECT' / ('UNION' (__ 'ALL')?);
set_quantifier <- 
	('DISTINCT' !r'[A-Z]') / ('ALL' !r'[A-Z]');
set_limit <-
	'TOP' __ unsigned_integer;
select_list	<-
	select_item (_ ',' _ select_item)* / '*';
select_item	<- 
	select_column correlation_specification?;
select_column <- 
	numeric_value_expression
	/ operation
	/ concatenation_operation
	/ set_function_specification
	/ identifier
	/ math_function
	/ string_function
	/ string_literal
	/ geometry_function;
from_clause <-
	'FROM' __ from_list;
from_list <- 
	table_reference (_ ',' _ table_reference)*;
correlation_specification <-
	(_ 'AS' __)? identifier;
table_reference <-
	joined_table
	/ subquery correlation_specification
	/ identifier correlation_specification?;
joined_table <-
	qualified_join 
	/ sub_join;
sub_join <-
	'(' _ joined_table _ ')';
join_opener <-
	sub_join
	/ identifier correlation_specification?
	/ subquery correlation_specification;
qualified_join <-
	join_opener __ join_clause;
subquery <-
	'(' _ query_expression _ ')';
join_clause <- 
	join_method? 'JOIN' __ table_reference (__ join_specification)? (__ (sub_join / join_clause))*;
join_type <-  
	'INNER' 
	/ (('LEFT' /  'RIGHT' / 'FULL') __)? 'OUTER';
join_method	<-
	('NATURAL' __)? (join_type __)?;
join_specification	<- 
	'ON' __ '('? _ search_list _ ')'?
	/ 'USING' __ '(' _  identifier (_ ',' _ identifier)* _ ')';
where_clause <-
	'WHERE' __ search_list;
search_list	<-
	search_condition (__ ('AND' / 'OR') __ ( 'NOT' __)? search_condition)*;
search_condition <-
	operation
	/ exists_operation
	/ math_function
	/ geometry_function
	/ general_set_function;
search_column <-
	concatenation_operation
	/ numeric_value_expression
	/ math_function
	/ identifier
	/ general_set_function
	/ string_literal
	/ geometry_function;
operation <-
	search_column (__ unary_operation / _ binary_operation / __ ternary_operation / __ list_operation)
	/ '(' search_column (__ unary_operation / _ binary_operation / __ ternary_operation / __ list_operation) ')';
unary_operation <-
	'IS' (__ 'NOT')? __ ('TRUE' / 'FALSE' / 'UNKNOWN' / 'NULL');
binary_operation <-
	('NOT' __)? _ ("=" / "!=" / "<=" / ">=" / ">" / "<" / "ILIKE" / "LIKE") _ search_column (_ binary_operation)*;
ternary_operation <- 
	('NOT' __)? 'BETWEEN' __ search_column __ 'AND' __ search_column;
list_operation <-
	('NOT' __)? 'IN' __ ( regular_identifier  / '(' search_column (_ ',' _ search_column)* ')' / '(' query_expression ')');
exists_operation <-
	('NOT' __)? 'EXISTS' __ '(' query_expression ')';
order_by <-
	'ORDER BY' __ order_by_list;
order_by_list <-
	order_by_item (_ ',' _ order_by_item)*;
order_by_item <- 
	(identifier / unsigned_integer) (__ order_by_sort)?;
order_by_sort <- 
	'ASC' 
	/ 'DESC';
group_by <-
	'GROUP BY' __ group_by_list (__ having_clause)?;
group_by_list <-
	identifier (_ ',' _ identifier)*;
having_clause <-
	'HAVING' __ '('? search_list ')'?;
offset <-
	'OFFSET' __ unsigned_integer;
identifier <-
	((regular_identifier / delimited_identifier) _ "." _)? ((regular_identifier / delimited_identifier)
	_ "." _)? (regular_identifier / delimited_identifier / '*');
delimited_identifier <-
	'"' ('""' / r'[^"]')+ '"';
regular_identifier <-
	(!(reserved_keywords(!letter)) letter (letter / digit / "_")*);
string_literal <-
	"'" ("''" / r"[^']")* "'";
concatenation_operation <-
	(string_literal / identifier) _ '||' _ (string_literal / identifier);
geometry_function <-
	geometry_value_function
	/ non_predicate_geometry_function 
	/ predicate_geometry_function
	/ region
	/ coordsys;
geometry_value_function <-
	box
	/ centroid
	/ circle
	/ point
	/ polygon;
	// / user_defined_function;
coordinates <-
	numeric_value_expression _ ',' _ numeric_value_expression;
box <-
	'BOX' '(' (_ string_literal ',')? _ coordinates _ ',' _ function_param _ ','_ function_param _ ')';
centroid <-
	'CENTROID' '(' _ function_param _ ')';
circle <-
	'CIRCLE' '(' ( _ string_literal _ ',')? _ coordinates _ ',' _ numeric_value_expression _ ')';
polygon <-
	'POLYGON' '(' ( _ string_literal _ ',')? _ coordinates _ ',' _ coordinates _ (_ ',' _ coordinates)* _ ')';
non_predicate_geometry_function <-
	'AREA' '(' _  function_param _ ')'
	/ 'COORD1' '(' _ function_param _ ')'
	/ 'COORD2' '(' _ function_param _ ')'
	/ 'DISTANCE' '(' _ function_param _ ',' _ function_param _ ')'
	/ 'DISTANCE' '(' _ function_param _ ',' function_param _ ',' _ function_param _ ',' _ function_param _ ')';
predicate_geometry_function <-
	contains
	/ intersects;
contains <- 
	'CONTAINS' '(' _ function_param _ ',' _ function_param _ ')';
intersects <-
	'INTERSECTS' '(' _ function_param _ ',' _ function_param _ ')';
region <-
	'REGION' '(' _  string_literal _ ')';
coordsys <-
	'COORDSYS' '(' _ identifier _ ')';
function_param <-
	geometry_function 
	/ numeric_value_expression 
	/ math_function;
math_function <-
	(onetwo_param_math '(' _ function_param (_ ',' _ signed_integer)? _ ')')
	/ (two_param_math '(' _  function_param _ ',' _ function_param _')')
	/ (one_param_math '(' _ function_param _ ')')
	/ rand_function
	/ (no_param_math '()');
// No params
no_param_math <-
	'PI';
// One param 
one_param_math <-
	'ABS' / 'CEILING' / 'DEGREES' / 'EXP' / 'FLOOR' / 'LOG10' / 'LOG' / 'RADIANS' / 'SQRT' / 'ACOS'
	/ 'ASIN' / 'ATAN' / 'COS' / 'COT' / 'SIN' / 'TAN';
// Two param
two_param_math <-
	'MOD' / 'POWER' / 'ATAN2';
onetwo_param_math <- 
	'ROUND' / 'TRUNCATE';
rand_function <-
	'RAND' '(' (_ unsigned_integer _)? ')';
string_function <-
	string_func_name '(' _ search_column _ ')';
string_func_name <-
	'LOWER';
function_name <-
	'ABS' / 'ACOS' / 'AREA' / 'ASIN' / 'ATAN' / 'ATAN2' / 'BOX' / 'CEILING' / 'CENTROID' / 'CIRCLE'
	/ 'COS' / 'DEGREES' / 'EXP' / 'FLOOR' / 'LOG' / 'LOG10' / 'MOD' / 'PI' / 'POINT' / 'POLYGON'
	/ 'POWER' / 'RADIANS' / 'REGION' / 'RAND' / 'ROUND' / 'SIN' / 'SQRT' / 'TAN';
set_function_specification <-
	'COUNT' '(' ('*' / (set_quantifier __)? identifier) ')' / general_set_function;
set_function_type <-
	'AVG' / 'MAX' / 'MIN' / 'SUM'; 
general_set_function <-
	set_function_type '(' (set_quantifier __)? numeric_value_expression ')';
point <-
	'POINT' '(' ( _ (string_literal / 'NULL') _ ',')? _ coordinates _ ')';
comment	<-
	'--' r'[^\n\r]*';
comments <-
	(comment EndOfLine)+;
numeric_value_expression <-
 	term (_ ('+' / '-') _ numeric_value_expression)*;
term <-
	factor (_ ('*' / '/') _ term)*;
factor <-
	('+' / '-')? numeric_primary;
numeric_value_function <-  // TODO!  this isn't right! (UDF?  trig?)
	math_function;
numeric_primary	<-
	value_expression_primary
	/ numeric_value_function;
value_expression_primary <-
	'(' numeric_value_expression ')'
	/ unsigned_integer
	/ identifier
	/ set_function_specification;
numeric_value <-
	scientific_number 
	/ decimal 
	/ unsigned_integer;
scientific_number <- 
	(decimal / unsigned_integer) 'E' ('+' / '-')? unsigned_integer;
decimal	<-
	unsigned_integer '.' digit+;
signed_integer <-
	('+' / '-')? unsigned_integer;
unsigned_integer <-
	digit+;
digit <-
	r'[0-9]';
letter <-
	r'[a-zA-Z]';
reserved_keywords <-
	'SELECT' / 'FROM' / 'WHERE' / 'ORDER BY' / 'DISTINCT' / 'ALL' / 'TOP' / 'AS' / 'ASC' / 'DESC' / '='
	/ '!=' / '<=' / '>=' / '>' / '<' / 'LIKE' / 'ILIKE' / 'OR' / 'AND' / 'JOIN' / 'ON' / 'INNER' 
	/ 'OUTER' / 'LEFT' / 'RIGHT' / 'OUTER' / 'NATURAL' / 'ON' / 'USING' / 'OFFSET' / 'GROUP BY'
	/ 'HAVING' / '||' / 'EXISTS' / 'UNKNOWN' / 'NULL' / 'NOT' / 'BIT_AND' / 'BIT_NOT' / 'BIT_OR' 
	/ 'BIT_XOR' / 'CONTAINS' / 'COORD1' / 'COORD2' / 'COORDSYS' / 'DISTANCE' / 'INTERSECTS' / 'IN_UNIT' 
	/ 'TRUNCATE' /  'COUNT' / function_name / set_function_type / string_func_name / 'EXCEPT' 
	/ 'INTERSECT' / 'UNION' / ADQL_reserved_word; 
ADQL_reserved_word <-
	'ABSOLUTE' / 'ACTION' / 'ADD' / 'ALL' / 'ALLOCATE' / 'ALTER' / 'ANY' / 'ARE' / 'ASSERTION' / 'AT'
	/ 'AUTHORISATION' / 'BEGIN' / 'BIT' / 'BIT_LENGTH' / 'BOTH' / 'BY' / 'CASCADE' / 'CASCADED' / 'CASE'
	/ 'CAST' / 'CATALOG' / 'CHAR' / 'CHARACTER' / 'CHAR_LENGTH' / 'CHARACTER_LENGTH' / 'CHECK' / 'CLOSE'
	/ 'COALESCE' / 'COLLATE' / 'COLLATION' / 'COLUMN' / 'COMMIT' / 'CONNECT' / 'CONNECTION'
	/ 'CONSTRAINT' / 'CONSTRAINTS' / 'CONTINUE' / 'CONVERT' / 'CORRESPONDING' / 'CREATE'
	/ 'CROSS' / 'CURRENT' / 'CURRENT_DATE' / 'CURRENT_TIME' / 'CURRENT_TIMESTAMP' / 'CURRENT_USER'
	/ 'CURSOR' / 'DATE' / 'DAY' / 'DEALLOCATE' / 'DECIMAL' / 'DECLARE' / 'DEFAULT' / 'DEFERRABLE'
	/ 'DEFERRED' / 'DELETE' / 'DESCRIBE' / 'DESCRIPTOR' / 'DIAGNOSTICS' / 'DISCONNECT' / 'DISTINCT'
	/ 'DOMAIN' / 'DOUBLE' / 'DROP' / 'ELSE' / 'END' / 'EXCEPT' / 'EXCEPTION' / 'EXEC' / 'EXECUTE'
	/ 'EXISTS' / 'EXTERNAL' / 'EXTRACT' / 'FALSE' / 'FETCH' / 'FIRST' / 'FLOAT' / 'FOR' / 'FOREIGN'
	/ 'FOUND' / 'FROM' / 'FULL' / 'GET' / 'GLOBAL' / 'GO' / 'GOTO' / 'GRANT' / 'GROUP' / 'HAVING'
	/ 'HOUR' / 'IDENTITY' / 'IMMEDIATE' / 'IN' / 'INDICATOR' / 'INITIALLY' / 'INNER' / 'INPUT'
	/ 'INSENSITIVE' / 'INSERT' / 'INT' / 'INTEGER' / 'INTERSECT' / 'INTERVAL' / 'INTO' / 'IS'
	/ 'ISOLATION' / 'JOIN' / 'KEY' / 'LANGUAGE' / 'LAST' / 'LEADING' / 'LEFT' / 'LEVEL' / 'LIKE'
	/ 'ILIKE' / 'LOCAL' / 'LOWER' / 'MATCH' / 'MAX' / 'MIN' / 'MINUTE' / 'MODULE' / 'MONTH' / 'NAMES'
	/ 'NATIONAL' / 'NATURAL' / 'NCHAR' / 'NEXT' / 'NO' / 'NOT' / 'NULL' / 'NULLIF' / 'NUMERIC'
	/ 'OCTET_LENGTH' / 'OF' / 'ON' / 'ONLY' / 'OPEN' / 'OPTION' / 'OR' / 'ORDER' / 'OUTER' / 'OUTPUT'
	/ 'OVERLAPS' / 'PAD' / 'PARTIAL' / 'POSITION' / 'PRECISION' / 'PREPARE' / 'PRESERVE' / 'PRIMARY'
	/ 'PRIOR' / 'PRIVILEGES' / 'PROCEDURE' / 'PUBLIC' / 'READ' / 'REAL' / 'REFERENCES' / 'RELATIVE'
	/ 'RESTRICT' / 'REVOKE' / 'RIGHT' / 'ROLLBACK' / 'ROWS' / 'SCHEMA' / 'SCROLL' / 'SECOND'
	/ 'SECTION' / 'SESSION_USER' / 'SET' / 'SIZE' / 'SMALLINT' / 'SOME' / 'SPACE' / 'SQL' / 'SQLCODE'
	/ 'SQLERROR' / 'SQLSTATE' / 'SUBSTRING' / 'SUM' / 'SYSTEM_USER' / 'TABLE' / 'TEMPORARY' / 'THEN'
	/ 'TIME' / 'TIMESTAMP' / 'TIMEZONE_HOUR' / 'TIMEZONE_MINUTE' / 'TO' / 'TRAILING' / 'TRANSACTION'
	/ 'TRANSLATE' / 'TRANSLATION' / 'TRIM' / 'TRUE' / 'UNIQUE' / 'UNKNOWN' / 'UPDATE' / 'UPPER'
	/ 'USAGE' / 'USER' / 'USING' / 'VALUE' / 'VALUES' / 'VARCHAR' / 'VARYING' / 'VIEW' / 'WHEN'
	/ 'WHENEVER' / 'WITH' / 'WORK' / 'WRITE' / 'ZONE';
ANY_CHAR <-
	letter / digit / ' ' / '\t' / ',' / ';' / '.';
_ <-
	(comment / Space / EOL)*;
__ <-
	(comment / Space / EOL)+;
Space <-
	' '+ / '\t';
EOL <-
	'\r\n' / '\n' / '\r';
