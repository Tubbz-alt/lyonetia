query			<- _ select __ from (__ join)? (__ where)? (__ order_by)? (__ group_by)? (__ having)?;
select			<- 'SELECT' (__ ('DISTINCT' / 'ALL'))? (__ select_limit)? __ select_list;
select_limit		<- 'TOP' __ unsigned_integer;
select_list		<- select_item (_ ',' _ select_item)*;
select_item		<- select_column (__ ('AS' __)? identifier)?;
select_column		<- subquery / '*' / identifier / numeric_expression / where_item;
from			<- 'FROM' __ (subquery /  from_list);
from_list		<- table_item (_ ',' _ table_item)*;
subquery		<- '(' _ query _ ')' (__ 'AS')? __ identifier;
join			<- '('? join_method? 'JOIN' __ ((identifier join_condition) ')'? / '(' query ')'); 
join_method		<- 'NATURAL'? 'INNER' / (('LEFT' /  'RIGHT' / 'FULL') _ 'OUTER'?);
join_condition		<- 'ON' __ where_list / 'USING' _ '(' identifier (_ ',' identifier)* ')';
where			<- 'WHERE' __ where_list;
where_list		<- where_item (__ ('AND' / 'OR') __ where_item)*;
where_item		<- binary_where_item / ternary_where_item;
where_column		<- identifier / numeric_expression;
binary_where_item	<- where_column _ ("=" / "!=" / "<=" / ">=" / ">" / "<" / "I"?"LIKE" / "||") _ where_column;
ternary_where_item	<- identifier __ 'BETWEEN' identifier 'AND' identifier;
table_item		<- identifier (__ ('AS' __)? identifier)?;
order_by		<- 'ORDER BY' __ order_by_list;
order_by_list		<- order_by_item (_ ',' _ order_by_item)*;
order_by_item		<- (identifier / unsigned_integer) (__ order_by_sort)?;
order_by_sort		<- 'ASC' / 'DESC';
group_by		<- 'GROUP BY' __ identifier (_ ',' identifier)*;
having			<- 'HAVING' '(' _ where_item _ ')';
identifier		<- (regular_identifier / delimited_identifier) ("." (regular_identifier / delimited_identifier) / '*')?;
delimited_identifier	<- r'"' (r'\\"' / r'[^"]')* r'"';
regular_identifier	<- (!reserved_keywords letter (letter / digit / "_")*);
comment			<- "-" "-" r'.*\n';
numeric_expression	<- term (_ ('+' / '-') _ numeric_expression)?;
term			<- factor (_ ('*' / '/') _ term)?;
factor			<- ('+' / '-')? numeric_primary;
numeric_primary		<- numeric_value / identifier;
numeric_value		<- scientific_number / decimal / unsigned_integer;
scientific_number	<- (decimal / unsigned_integer) 'E' ('+' / '-')? unsigned_integer;
decimal			<- unsigned_integer '.' digit+;
unsigned_integer	<- digit+;
digit			<- r'[0-9]';
letter			<- r'[a-zA-Z]';
reserved_keywords	<- 'SELECT' / 'FROM' / 'WHERE' / 'ORDER BY' / 'DISTINCT' / 'ALL' / 'TOP' / 'AS' / 'ASC' / 'DESC' / '=' / '!=' / '<=' / '>=' / '>' / '<' / 'LIKE' / 'ILIKE' / 'OR' / 'AND' / 'JOIN' / 'ON' / 'INNER' / 'OUTER' / 'LEFT' / 'RIGHT' / 'OUTER' / 'ON' / 'USING' / 'OFFSET' / 'GROUP BY' / 'HAVING' / '||';
ANY_CHAR		<- letter / digit / ' ' / '\t' / ',' / ';' / '.';
_			<- comment / Space* / EOL*;
__			<- comment / Space+ / EOL*;
Space			<- ' '+ / '\t';
EOL			<- '\r\n' / '\n' / '\r';
